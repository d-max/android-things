ext.projectModules = [
    server              : project(":server"),
    controller          : project(":controller"),
    firmware            : project(":firmware"),
]

ext.configureDependencies = { project ->
    project.dependencies {
        def module = project.name
        rootProject."${module}ModuleDeps".each {
            add(it.configuration, it.dependency, it.options)
        }
    }
}

ext.configureAndroidApplication = { project ->
    project.apply plugin: "com.android.application"
    configureAndroidModule(project)
}

ext.configureAndroidLibrary = { project ->
    project.apply plugin: "com.android.library"
    configureAndroidModule(project)
}

ext.configureKotlinModule = { project ->
    project.apply plugin: 'kotlin'
    project.apply plugin: 'kotlin-kapt'
    project.sourceSets {
        // this is necessary for test assests to be visible
        test { output.resourcesDir = "build/classes/main" }
    }
    project.test {
        reports.html.setDestination(file("$rootProject.buildDir/reports/tests"))
    }
    project.compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

def configureAndroidModule(project) {
    project.apply plugin: 'kotlin-android'
    project.apply plugin: 'kotlin-android-extensions'
    project.apply plugin: 'kotlin-kapt'

    project.android {
        compileSdkVersion rootProject.ext.compileSdkVersion

        defaultConfig {
            minSdkVersion rootProject.ext.minSdkVersion
            targetSdkVersion rootProject.ext.targetSdkVersion
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = "1.8"
        }

        sourceSets {
            main.java.srcDirs += 'src/main/kotlin'
            test.java.srcDirs += 'src/test/kotlin'
        }
    }
    project.androidExtensions {
        experimental = true
    }
}
