ext.projectModules = [
    
    server              : project(":server"),

    domain              : project(":common:domain"),
    gateway             : project(":common:gateway"),
    messaging           : project(":common:messaging"),

    controllerApp       : project(":controller:app"),
    controllerCore      : project(":controller:core"),

    firmwareApp         : project(":firmware:app"),
    firmwareCore        : project(":firmware:core"),
    math                : project(":firmware:math"),
    mechanics           : project(":firmware:mechanics"),
    hardware            : project(":firmware:hardware"),
]

ext.configureDependencies = { project ->
    project.dependencies {
        def prefix = project.parent.name
        def module = project.name
        rootProject."${prefix}"["${module}ModuleDeps"].each {
            add(it.configuration, it.dependency, it.options)
        }
    }
}

ext.configureAndroidApplication = { project ->
    project.apply plugin: "com.android.application"
    configureAndroidModule(project)
}

ext.configureAndroidLibrary = { project ->
    project.apply plugin: "com.android.library"
    configureAndroidModule(project)
}

ext.configureKotlinModule = { project ->
    project.apply plugin: 'kotlin'
    project.apply plugin: 'kotlin-kapt'
    project.sourceSets {
        // this is necessary for test assests to be visible
        test { output.resourcesDir = "build/classes/main" }
    }
    project.test {
        reports.html.setDestination(file("$rootProject.buildDir/reports/tests"))
    }
    project.compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

def configureAndroidModule(project) {
    project.apply plugin: 'kotlin-android'
    project.apply plugin: 'kotlin-android-extensions'
    project.apply plugin: 'kotlin-kapt'

    project.android {
        compileSdkVersion rootProject.ext.compileSdkVersion

        defaultConfig {
            minSdkVersion rootProject.ext.minSdkVersion
            targetSdkVersion rootProject.ext.targetSdkVersion
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = "1.8"
        }

        sourceSets {
            main.java.srcDirs += 'src/main/kotlin'
            test.java.srcDirs += 'src/test/kotlin'
        }
    }
    project.androidExtensions {
        experimental = true
    }
}
